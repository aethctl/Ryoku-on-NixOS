import QtQuick
import Quickshell
import Quickshell.Io
import Ryoku.Ui.Singletons

Item {
    id: root

    required property PickerState state

    anchors.fill: parent

    readonly property bool shown: root.state ? root.state.riceWorkshopOpen : false
    readonly property var rice: root.state ? root.state.riceEntry : null

    readonly property string slug: rice ? String(rice.slug || "") : ""
    readonly property string name: rice ? String(rice.name || rice.slug || "") : ""
    readonly property string author: rice ? String(rice.author || "") : ""
    readonly property string blurb: rice ? String(rice.blurb || "") : ""
    readonly property string compat: rice ? String(rice.compat || "") : ""
    readonly property string createdWith: rice ? String(rice.createdWith || "") : ""
    readonly property string preview: rice ? String(rice.preview || "") : ""
    readonly property bool live: rice ? rice.live === true : false
    readonly property bool active: rice ? rice.active === true : false
    readonly property var tags: (rice && rice.tags)
        ? (Array.isArray(rice.tags) ? rice.tags : String(rice.tags).split(","))
        : []

    property real reveal: 0
    states: State { name: "open"; when: root.shown; PropertyChanges { target: root; reveal: 1 } }
    Behavior on reveal { NumberAnimation { duration: Theme.standard; easing.type: Theme.revealEasing } }
    visible: reveal > 0.01

    function close() { if (root.state) root.state.riceWorkshopOpen = false }
    Keys.onEscapePressed: root.close()

    onShownChanged: {
        if (root.shown) { root._loadTouches(); pickField.mode = ""; forceActiveFocus() }
    }

    ListModel { id: touchModel }
    property string _touchBuf: ""
    function _loadTouches() {
        touchModel.clear()
        _touchBuf = ""
        if (root.slug === "") return
        filesProc.command = ["ryoku-hub", "rice", "files", root.slug]
        filesProc.running = true
    }
    Process {
        id: filesProc
        stdout: SplitParser { splitMarker: ""; onRead: data => root._touchBuf += data }
        onExited: {
            touchModel.clear()
            var d = null
            try { d = JSON.parse(root._touchBuf) } catch (e) { d = null }
            var t = (d && d.touches) ? d.touches : []
            for (var i = 0; i < t.length; ++i)
                if (t[i].provided === true)
                    touchModel.append({ touchLabel: String(t[i].label || "") })
        }
    }

    function _run(args) { Quickshell.execDetached(args) }
    function applyRice() {
        if (root.slug === "") return
        root._run(["ryoku-hub", "rice", "apply", root.slug, "all"])
        if (root.state) root.state.toast(I18n.tr("Applying rice"), "info")
        root.close()
    }
    function forkRice() {
        if (root.slug === "") return
        root._run(["ryoku-hub", "rice", "fork", root.slug])
        if (root.state) root.state.toast(I18n.tr("Forked rice"), "success")
    }
    function restoreRice() {
        root._run(["ryoku-hub", "rice", "restore"])
        if (root.state) root.state.toast(I18n.tr("Restoring your desktop"), "info")
    }
    function deleteRice() {
        if (root.slug === "") return
        root._run(["ryoku-hub", "rice", "delete", root.slug])
        if (root.state) root.state.toast(I18n.tr("Deleted rice"), "success")
        root.close()
    }
    function captureLook() {
        var nm = root.name !== "" ? root.name : "rice"
        root._run(["ryoku-hub", "rice", "capture", nm, "all"])
        if (root.state) root.state.toast(I18n.tr("Capturing the current desktop"), "info")
    }
    function commitPath(folder) {
        if (folder === "") return
        if (pickField.mode === "export") {
            if (root.slug !== "") root._run(["ryoku-hub", "rice", "export", root.slug, folder])
            if (root.state) root.state.toast(I18n.tr("Exporting rice"), "info")
        } else if (pickField.mode === "import") {
            root._run(["ryoku-hub", "rice", "import", folder])
            if (root.state) root.state.toast(I18n.tr("Importing rice"), "info")
        }
        pickField.mode = ""
        pickField.text = ""
    }

    Scrim {
        anchors.fill: parent
        alpha: 0.55
        reveal: root.reveal
        onDismissed: root.close()
    }

    Rectangle {
        id: card
        anchors.centerIn: parent
        width: Math.min(parent.width - 100 * Theme.scale, 880 * Theme.scale)
        height: Math.min(parent.height - 100 * Theme.scale, 500 * Theme.scale)
        opacity: root.reveal
        transform: Translate { y: (1 - root.reveal) * 12 * Theme.scale }

        color: Theme.withAlpha(Theme.surface, 0.99)
        border.width: 1
        border.color: Theme.withAlpha(Theme.outline, 0.58)

        MouseArea { anchors.fill: parent }

        Text {
            anchors.top: parent.top
            anchors.right: parent.right
            anchors.topMargin: 12 * Theme.scale
            anchors.rightMargin: 14 * Theme.scale
            text: "\u00d7"
            font.family: Theme.ui; font.weight: Theme.uiWeight
            font.pixelSize: Theme.fontTitle
            color: closeMouse.containsMouse ? Theme.surfaceText : Theme.withAlpha(Theme.surfaceText, 0.7)
            z: 5
            renderType: Text.NativeRendering
            MouseArea {
                id: closeMouse
                anchors.fill: parent
                anchors.margins: -8 * Theme.scale
                hoverEnabled: true
                cursorShape: Qt.PointingHandCursor
                onClicked: root.close()
            }
        }

        Row {
            anchors.fill: parent
            anchors.margins: 22 * Theme.scale
            spacing: 22 * Theme.scale

            Rectangle {
                id: preview
                width: (parent.width - parent.spacing) * 0.42
                height: parent.height
                clip: true
                color: Theme.withAlpha(Theme.surfaceContainer, 0.9)

                Image {
                    anchors.fill: parent
                    visible: root.preview !== "" && status === Image.Ready
                    source: Library.fileUrl(root.preview)
                    fillMode: Image.PreserveAspectCrop
                    asynchronous: true
                    cache: false
                    sourceSize.width: 560
                    sourceSize.height: 680
                }
                Text {
                    anchors.centerIn: parent
                    visible: root.preview === ""
                    text: root.name.length > 0 ? root.name.charAt(0).toUpperCase() : "?"
                    font.family: Theme.ui
                    font.pixelSize: 120 * Theme.scale
                    color: Theme.withAlpha(Theme.surfaceText, 0.22)
                    renderType: Text.NativeRendering
                }
                Rectangle {
                    anchors.fill: parent
                    color: "transparent"
                    border.width: 1
                    border.color: Theme.withAlpha(Theme.outline, 0.4)
                }
            }

            Item {
                width: parent.width - preview.width - parent.spacing
                height: parent.height

                Column {
                    id: infoCol
                    anchors.top: parent.top
                    anchors.left: parent.left
                    anchors.right: parent.right
                    spacing: 8 * Theme.scale

                    Text {
                        visible: root.author !== ""
                        text: root.author.toUpperCase()
                        font.family: Theme.ui; font.weight: Theme.uiWeight
                        font.pixelSize: Theme.fontFine
                        font.letterSpacing: 2
                        color: Theme.withAlpha(Theme.surfaceText, 0.6)
                        renderType: Text.NativeRendering
                    }
                    Text {
                        width: parent.width
                        text: root.name
                        font.family: Theme.ui; font.weight: Theme.uiWeight
                        font.pixelSize: Theme.fontSegment
                        color: Theme.surfaceText
                        elide: Text.ElideRight
                        maximumLineCount: 1
                        renderType: Text.NativeRendering
                    }
                    Flow {
                        width: parent.width
                        spacing: 6 * Theme.scale
                        RiceChip { visible: root.compat !== ""; label: root.compat.toUpperCase(); strong: true }
                        RiceChip { visible: root.createdWith !== ""; label: "v" + root.createdWith }
                        RiceChip { visible: root.live; label: I18n.tr("LIVE"); tint: Theme.primary; strong: true }
                        RiceChip { visible: root.active; label: I18n.tr("ACTIVE"); tint: Theme.primary; strong: true }
                        Repeater {
                            model: root.tags
                            delegate: RiceChip {
                                required property var modelData
                                label: String(modelData).trim()
                                visible: label.length > 0
                            }
                        }
                    }
                    Text {
                        visible: root.blurb !== ""
                        width: parent.width
                        text: root.blurb
                        wrapMode: Text.WordWrap
                        maximumLineCount: 3
                        elide: Text.ElideRight
                        font.family: Theme.ui; font.weight: Theme.uiWeight
                        font.pixelSize: Theme.fontBase
                        color: Theme.withAlpha(Theme.surfaceText, 0.62)
                        renderType: Text.NativeRendering
                    }
                    Text {
                        text: I18n.tr("Touches")
                        font.family: Theme.ui; font.weight: Theme.uiWeight
                        font.pixelSize: Theme.fontTiny
                        font.letterSpacing: 1.5
                        color: Theme.withAlpha(Theme.surfaceText, 0.52)
                        renderType: Text.NativeRendering
                    }
                    Flow {
                        width: parent.width
                        spacing: 6 * Theme.scale
                        Repeater {
                            model: touchModel
                            delegate: RiceChip {
                                required property string touchLabel
                                label: touchLabel
                            }
                        }
                        Text {
                            visible: touchModel.count === 0
                            text: "\u2014"
                            font.family: Theme.ui; font.weight: Theme.uiWeight
                            font.pixelSize: Theme.fontBase
                            color: Theme.withAlpha(Theme.surfaceText, 0.4)
                            renderType: Text.NativeRendering
                        }
                    }
                }

                Column {
                    anchors.bottom: parent.bottom
                    anchors.left: parent.left
                    anchors.right: parent.right
                    spacing: 10 * Theme.scale

                    Row {
                        visible: pickField.mode !== ""
                        width: parent.width
                        spacing: 8 * Theme.scale
                        TextField {
                            id: pickField
                            property string mode: ""
                            width: parent.width - goBtn.width - cancelBtn.width - 16 * Theme.scale
                            variant: "field"
                            placeholder: pickField.mode === "export"
                                ? I18n.tr("Folder to export into")
                                : I18n.tr("Folder to import from")
                            onCommitted: root.commitPath(text.trim())
                        }
                        FolioAction {
                            id: goBtn
                            label: I18n.tr("Go")
                            onTriggered: root.commitPath(pickField.text.trim())
                        }
                        FolioAction {
                            id: cancelBtn
                            label: I18n.tr("Cancel")
                            onTriggered: { pickField.mode = ""; pickField.text = "" }
                        }
                    }

                    Row {
                        spacing: 8 * Theme.scale
                        FixedButton {
                            mode: "action"
                            label: I18n.tr("Apply")
                            active: true
                            onTriggered: root.applyRice()
                        }
                        FixedButton {
                            mode: "action"
                            label: I18n.tr("Fork")
                            onTriggered: root.forkRice()
                        }
                        FixedButton {
                            mode: "action"
                            label: I18n.tr("Restore")
                            onTriggered: root.restoreRice()
                        }
                        FolioDestructiveAction {
                            label: I18n.tr("Delete")
                            confirm: true
                            onTriggered: root.deleteRice()
                        }
                    }

                    Text {
                        text: I18n.tr("Share")
                        font.family: Theme.ui; font.weight: Theme.uiWeight
                        font.pixelSize: Theme.fontTiny
                        font.letterSpacing: 1.5
                        color: Theme.withAlpha(Theme.surfaceText, 0.52)
                        renderType: Text.NativeRendering
                    }

                    Row {
                        spacing: 8 * Theme.scale
                        FixedButton {
                            mode: "action"
                            label: I18n.tr("Save look")
                            onTriggered: root.captureLook()
                        }
                        FixedButton {
                            mode: "action"
                            label: I18n.tr("Export")
                            enabled: root.slug !== ""
                            onTriggered: { pickField.mode = "export"; pickField.text = ""; pickField.forceActiveFocus() }
                        }
                        FixedButton {
                            mode: "action"
                            label: I18n.tr("Import")
                            onTriggered: { pickField.mode = "import"; pickField.text = ""; pickField.forceActiveFocus() }
                        }
                    }
                }
            }
        }
    }

    component RiceChip: Rectangle {
        id: chip
        property string label: ""
        property color tint: Theme.withAlpha(Theme.surfaceText, 0.7)
        property bool strong: false
        implicitWidth: chipText.implicitWidth + 16 * Theme.scale
        height: 20 * Theme.scale
        color: chip.strong ? Theme.withAlpha(chip.tint, 0.16) : "transparent"
        border.width: 1
        border.color: Theme.withAlpha(chip.tint, chip.strong ? 0.55 : 0.3)
        Text {
            id: chipText
            anchors.centerIn: parent
            text: chip.label
            font.family: Theme.ui; font.weight: Theme.uiWeight
            font.pixelSize: Theme.fontFine
            font.letterSpacing: 0.8
            color: chip.strong ? chip.tint : Theme.withAlpha(Theme.surfaceText, 0.7)
            renderType: Text.NativeRendering
        }
    }
}
